use Libraries.Compute.Statistics.DataFrame
use Libraries.Game.Game
use Libraries.Containers.Array
use Libraries.Interface.Controls.Charts.Chart
use Libraries.Interface.Layouts.ManualLayout

/*
    SVG generator for online tutorials.
*/

class Main is Game

    Array<Test> chartsArr
    Chart currentChart = undefined
    text currentName = ""
    
    integer index = 0
    integer svgDelay = 0
    integer delay = 5

    action Main
        StartGame()
    end

    action CreateGame     

        ManualLayout layout
        SetLayout(layout)

        SetScreenSize(1000,1000)
        LoadCharts()
        
        //other chart types should go in their own array.
        currentChart = chartsArr:Get(index):GenerateChart()
        currentName = chartsArr:Get(index):GetName()
        Add(currentChart)
    end

    action LoadCharts

        BarChartGenerator barChartGenerator
        HistogramGenerator histogramGenerator
        PieChartGenerator pieChartGenerator
        LineChartGenerator lineChartGenerator
        ScatterPlotGenerator scatterPlotGenerator
        BoxPlotGenerator boxPlotGenerator
        ViolinPlotGenerator violinPlotGenerator        

        chartsArr:Add(barChartGenerator)
        chartsArr:Add(histogramGenerator)
        chartsArr:Add(pieChartGenerator)
        chartsArr:Add(lineChartGenerator)
        chartsArr:Add(scatterPlotGenerator)
        chartsArr:Add(boxPlotGenerator)
        chartsArr:Add(violinPlotGenerator)
    end

    action Update(number seconds)
        if svgDelay > delay and index < chartsArr:GetSize() - 1 
            index = index + 1
            currentChart:SetColorPaletteToColorgorical() // Reset colors
            Remove(currentChart)
            currentChart = chartsArr:Get(index):GenerateChart()
            currentName = chartsArr:Get(index):GetName()
            Add(currentChart)
            svgDelay = 0
        else 
            svgDelay = svgDelay + 1
            currentChart:Save("/SourceCode/" +currentName+ ".svg")
            if index >=chartsArr:GetSize() - 1  and svgDelay > delay
                Exit()
            end
        end
        
    end
    

end



