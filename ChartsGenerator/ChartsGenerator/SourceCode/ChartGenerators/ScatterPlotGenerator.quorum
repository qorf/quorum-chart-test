use Libraries.Compute.Statistics.DataFrame
use Libraries.Interface.Controls.Charts.ScatterPlot
use Libraries.Interface.Controls.Charts.Chart

class ScatterPlotGenerator is Test
    text name = "scatterplot1"
    action GetName returns text
        return name
    end

    action GenerateChart returns Chart
        
        /*
            This is an example of a simple scatter plot in quorum.
            The data collected is about medical insurance costs in relation to BMI (body mass index).
        */
        
        // Create a dataframe to hold the data.
        DataFrame frame
        
        // Load your data file into the frame.
        frame:Load("/SourceCode/Data/Science/insurance.csv")
        
        // Select data from the frame that you wish to use in your scatter plot. 
        // Note: In Scatter Plots, you must select at least two columns.
        //      The first column will be the x-axis independent variable.
        //      The second column or more will be the y-axis dependent variable.
        // For this example, we will be categorizing by "age" and plotting coordinates by "bmi" and "charges".
        frame:AddSelectedColumns("bmi")
        frame:AddSelectedColumns("charges")
        frame:AddSelectedFactors("age")
        
        // Using the frame, create a ScatterPlot object.
        ScatterPlot chart = frame:ScatterPlot()
        
        // Give the chart a descriptive title.
        chart:SetTitle("Charges of Insurance Based Off of BMI and Age")
        
        // Add a subtitle for further description.
        chart:SetSubtitle("Does body weight and age affect cost of insurance?")
        
        // Give the x axis a descriptive title.
        chart:SetXAxisTitle("Body Mass Index (BMI)")
        
        // Give the y axis a descriptive title.
        chart:SetYAxisTitle("Insurance Cost (in $)")
        
        // Give the legend a descriptive title.
        chart:SetLegendTitle("Age Group")
        
        // You can show linear regression line trends.
        chart:ShowLinearRegression(true) 
        
        // You can switch the x-axis and the y-axis.
        chart:FlipOrientation()
        
        // If needed, you can change the color palette to a predefined palette or create a custom one.
        // Disturbing color palette contains yellows, oranges, browns, and greens
        chart:SetColorPaletteToDisturbing()
        
        return chart

    end
end