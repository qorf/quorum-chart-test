use Libraries.Compute.Statistics.DataFrame
use Libraries.Interface.Controls.Charts.BarChart
use Libraries.Interface.Controls.Charts.Chart


class BarChartGenerator is Test
    text name = "barcharttutorial"
    action GetName returns text
        return name
    end

    action GenerateChart returns Chart

        // Create a DataFrame to hold the data.
        DataFrame frame

        // Load your data file into the frame.
        frame:Load("/SourceCode/Data/Culture and Entertainment/WorldNightOut.csv")
        
        // Select data from the frame that you wish to use in your bar chart.
        // Note: In BarCharts, factors are used as independent data, and columns are used as dependent data.
        // For this example we will be categorizing by "Country" and "Item" and calculating the bar value by "Cost".
        frame:AddSelectedFactors("Country")
        frame:AddSelectedFactors("Item")
        frame:AddSelectedColumns("Cost")
        
        // Using the frame, create a BarChart object.
        BarChart chart = frame:BarChart()
        
        // Give the chart a descriptive title.
        chart:SetTitle("How Expensive are Night Outs Per Country?")
        
        // Add a subtitle for further description.
        chart:SetSubtitle("Various nightly activities cost in different countries")
        
        // Give the x axis a descriptive title.
        chart:SetXAxisTitle("Countries")
        
        // Give the y axis a descriptive title.
        chart:SetYAxisTitle("Cost (in USD $)")
        
        // You can adjust the y axis maximum or minimum.
        chart:SetYAxisMaximum(300)
        
        // If needed, you can change the color palette to a predefined palette or create a custom one.
        // By default, the palette is set to Colorgorical which helps give our graphs aesthetic and discriminable elements.
        chart:SetColorPaletteToColorgorical()
        
        // If needed, you can change the legend location to either left, right, top or bottom. 
        // By default, the legend is displayed on the right when showing.
        chart:SetLegendLocation("bottom")
        
        // Give the legend a descriptive title.
        chart:SetLegendTitle("Activity")

        chart:SetXLabelFontSize(12)
        

        return chart

    end
end