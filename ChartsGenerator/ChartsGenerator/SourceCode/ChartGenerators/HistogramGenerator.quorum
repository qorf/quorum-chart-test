use Libraries.Compute.Statistics.DataFrame
use Libraries.Interface.Controls.Charts.Histogram
use Libraries.Interface.Controls.Charts.Chart

class HistogramGenerator is Test
    text name = "histogramtutorial"
    action GetName returns text
        return name
    end

    action GenerateChart returns Chart       
        /*
            This is an example of a Histogram built in quorum.
            The dataset we will be working with compares AirBnB prices in NYC
        */
        
        // Create a DataFrame to hold the data.
        DataFrame frame
        
        // Load your data file into the frame.
        frame:Load("/SourceCode/Data/Miscellaneous/AB_NYC_2019.csv")

        // Select data from the frame that you wish to use in your histogram. 
        // Note: In Histograms, factors and non-numerical columns are not supported.
        // For this example we will be using the numerical column "price".
        frame:AddSelectedColumns("price")
        
        // Using the frame, create a Histogram object.
        Histogram chart = frame:Histogram()
        
        // Give the chart a descriptive title.
        chart:SetTitle("Price per night with AirBnB in 2019 (NYC)")
        
        // Add a subtitle for further description.
        chart:SetSubtitle("How expensive is it to stay in NYC?")
        
        // Give the x axis a descriptive title.
        chart:SetXAxisTitle("Price ($)")
        
        // Give the y axis a descriptive title.
        chart:SetYAxisTitle("Number of Stays")
        
        // You can hide the legend, as it is not needed for this example.
        chart:ShowLegend(false)
        
        // If needed, you can change the color palette to a predefined palette or create a custom one.
        chart:SetColorPaletteToWarmScale()
        
        // Set a custom interval along the x axis to 50 for a better display.
        // Alternatively, you can use SetBinWidth(50) for the same result.
        chart:SetXTickInterval(50)
        
        // Start the x axis at 0 for a better display.
        chart:SetXAxisMinimum(0)
        
        return chart

    end
end